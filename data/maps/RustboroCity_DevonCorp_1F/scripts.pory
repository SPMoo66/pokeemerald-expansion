raw `
RustboroCity_DevonCorp_1F_EventScript_RocksMetalDisplay::
	msgbox RustboroCity_DevonCorp_1F_Text_RocksMetalDisplay, MSGBOX_SIGN
	end

RustboroCity_DevonCorp_1F_EventScript_ProductsDisplay::
	msgbox RustboroCity_DevonCorp_1F_Text_ProductDisplay, MSGBOX_SIGN
	end

RustboroCity_DevonCorp_1F_Text_WelcomeToDevonCorp:
	.string "Hello and welcome to the Devon\n"
	.string "Corporation.\p"
	.string "We're proud producers of items and\n"
	.string "medicine that enhance your life.$"

RustboroCity_DevonCorp_1F_Text_StaffGotRobbed:
	.string "One of our research staff stupidly\n"
	.string "got robbed of an important parcel.$"

RustboroCity_DevonCorp_1F_Text_ThoseShoesAreOurProduct:
	.string "Hey, those Running Shoes!\n"
	.string "They're one of our products!\p"
	.string "It makes me happy when I see someone\n"
	.string "using something we made.$"

RustboroCity_DevonCorp_1F_Text_RobberWasntVeryBright:
	.string "That stolen parcel…\p"
	.string "Well, sure it's important, but it's not\n"
	.string "anything that anyone can use.\p"
	.string "In my estimation, that robber must not\n"
	.string "have been very bright.$"

RustboroCity_DevonCorp_1F_Text_SoundsLikeStolenGoodsRecovered:
	.string "It sounds like they've recovered\n"
	.string "the ripped-off Devon Goods.$"

RustboroCity_DevonCorp_1F_Text_OnlyAuthorizedPeopleEnter:
	.string "I'm sorry, only authorized people\n"
	.string "are allowed to enter here.$"

RustboroCity_DevonCorp_1F_Text_HowCouldWeGetRobbed:
	.string "It's beyond stupid.\n"
	.string "How could we get robbed?$"

RustboroCity_DevonCorp_1F_Text_YoureAlwaysWelcomeHere:
	.string "Hi, there!\n"
	.string "You're always welcome here!$"

RustboroCity_DevonCorp_1F_Text_RocksMetalDisplay:
	.string "Samples of rocks and metal are\n"
	.string "displayed in the glass case.\p"
	.string "There's a panel with some writing\n"
	.string "on it…\p"
	.string "“Devon Corporation got its start as\n"
	.string "a producer of stones from quarries.\p"
	.string "“The company also produced iron from\n"
	.string "filings in the sand.\p"
	.string "“From that humble start as a producer\n"
	.string "of raw materials, Devon developed.\p"
	.string "“Devon is now a manufacturer of a wide\n"
	.string "range of industrial products.”$"

RustboroCity_DevonCorp_1F_Text_ProductDisplay:
	.string "Prototypes and test products fill\n"
	.string "the glass display case.\p"
	.string "There's a panel with a description…\p"
	.string "“In addition to industrial products,\n"
	.string "Devon now markets sundries and\l"
	.string "pharmaceuticals for better lifestyles.\p"
	.string "“Recently, Devon has begun marketing\n"
	.string "tools for Pokémon Trainers, including\l"
	.string "Poké Balls and Pokénav systems.”$"
`

const EMPLOYEE = 1
const STAIR_GUARD = 2
const GREETER = 3
const LOOKER_DEVONCORP_1F = 5

mapscripts RustboroCity_DevonCorp_1F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (!flag(FLAG_RETURNED_DEVON_GOODS)) {
			setobjectxyperm (STAIR_GUARD, 21, 2)
			setobjectmovementtype (STAIR_GUARD, MOVEMENT_TYPE_FACE_DOWN)
			setflag(FLAG_TEMP_1)
		}
		elif (var(VAR_LOOKER_ROCKET) >= 3 && var(VAR_LOOKER_ROCKET) <= 5) {
			setobjectxyperm (STAIR_GUARD, 6, 5)
			setobjectmovementtype (STAIR_GUARD, MOVEMENT_TYPE_FACE_DOWN_AND_RIGHT)
			setobjectxyperm (EMPLOYEE, 0, 7)
			setobjectmovementtype (EMPLOYEE, MOVEMENT_TYPE_FACE_RIGHT)
			setobjectxyperm (GREETER, 1, 5)
		}
		else {
			setflag(FLAG_TEMP_1)
		}
		if (var(VAR_LOOKER_ROCKET) == 3) {
			getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
			if (var(VAR_TEMP_0) == 13) {
				setobjectxyperm (GREETER, 21, 6)
			}
			else {
				setobjectxyperm (GREETER, 20, 6)
			}
			savebgm (MUS_HG_ROCKET_TAKEOVER)
		}
		elif (var(VAR_LOOKER_ROCKET) == 4 || var(VAR_LOOKER_ROCKET) == 5) {
			savebgm (MUS_HG_ROCKET_TAKEOVER)
		}
		clearflag (FLAG_DONT_TRANSITION_MUSIC)
	}
	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_LOOKER_ROCKET) >= 3 && var(VAR_LOOKER_ROCKET) <= 5) {
			setmetatile (3, 3, 0x381, FALSE)
			setmetatile (3, 4, 0x381, FALSE)
			setmetatile (4, 3, 0x380, FALSE)
			setmetatile (4, 4, 0x380, FALSE)
			setmetatile (4, 5, 0x380, FALSE)
			setmetatile (5, 3, 0x380, FALSE)
			setmetatile (5, 4, 0x380, FALSE)
			setmetatile (5, 5, 0x380, FALSE)
			setmetatile (20, 3, 0x386, FALSE)
			setmetatile (20, 4, 0x38E, TRUE)
			setmetatile (21, 3, 0x387, FALSE)
			setmetatile (21, 4, 0x38F, TRUE)
			setmetatile (21, 5, 0x383, FALSE)
			setmetatile (22, 4, 0x384, FALSE)
			setmetatile (22, 5, 0x385, FALSE)
		}
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_LOOKER_ROCKET, 3: Devoncorp_RocketEntrance
	]
}

script Devoncorp_RocketEntrance {
	msgbox("Iyaaaaa!!")
	applymovement (GREETER, GreeterRun)
	waitmovement (0)
	closemessage
	applymovement (GREETER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	playse (SE_PIN)
	applymovement (GREETER, Common_Movement_ExclamationMark)
	waitmovement (0)
	delay (60)
	applymovement (GREETER, Common_Movement_WalkDown)
	waitmovement (0)
	msgbox(format("Oh! Please, you've got to help!\pA group of jackbooted thugs stormed in\nand went upstairs!\pI've got friends up there in other\ndepartments!\lOh no…"), MSGBOX_AUTOCLOSE)
	applymovement (GREETER, GreeterRun)
	waitmovement (0)
	setobjectxy (GREETER, 1, 5)
	applymovement (GREETER, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	addobject (LOOKER_DEVONCORP_1F)
	switch (var(VAR_TEMP_0)) {
		case 12:
			setobjectxy (LOOKER_DEVONCORP_1F, 13, 8)
		case 13:
			setobjectxy (LOOKER_DEVONCORP_1F, 12, 8)
		default:
	}
	applymovement (LOOKER_DEVONCORP_1F, Common_Movement_FaceUp)
	waitmovement (0)
	playse (SE_EXIT)
	waitse
	applymovement (LOOKER_DEVONCORP_1F, Common_Movement_WalkUp2)
	waitmovement (0)
	applymovement (LOOKER_DEVONCORP_1F, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	speakername (CommonText_Looker)
	msgbox (format ("{PLAYER}, it is good that you have arrived.\p"
					"Our adversaries are a group of villains I have not met before."))
	applymovement (LOOKER_DEVONCORP_1F, Common_Movement_WalkInPlaceFasterRight)
	waitmovement (0)
	msgbox (format ("I will try to take them on at the main staircase.\p"
					"If I cannot break through, try to find another path!\p"
					"We will meet up at the top and save Mr. Stone!"))
	applymovement (LOOKER_DEVONCORP_1F, LookerRunRight)
	waitmovement (0)
	setobjectxy (LOOKER_DEVONCORP_1F, 22, 5)
	applymovement (LOOKER_DEVONCORP_1F, Common_Movement_FaceUp)
	waitmovement (0)
	setvar (VAR_LOOKER_ROCKET, 4)
	release
}

movement GreeterRun {
	walk_fast_left * 8
}

movement LookerRunRight {
	walk_fast_right * 9
}

script RustboroCity_DevonCorp_1F_EventScript_StairGuard {
	lock
	faceplayer
	if (var(VAR_LOOKER_ROCKET) >= 3 && var(VAR_LOOKER_ROCKET) <= 5) {
		msgbox ("I saw a few of them go into the\nbasement here.\pIt's probably a safer bet than charging\nupstairs.\pI can watch over the others if you want\nto try it, but be careful.")
	}
	elif (flag(FLAG_RETURNED_DEVON_GOODS)) {
		msgbox (RustboroCity_DevonCorp_1F_Text_YoureAlwaysWelcomeHere, MSGBOX_DEFAULT)
	}
	elif (flag(FLAG_RECOVERED_DEVON_GOODS) || flag(FLAG_DEVON_GOODS_STOLEN)) {
		msgbox (RustboroCity_DevonCorp_1F_Text_HowCouldWeGetRobbed, MSGBOX_DEFAULT)
	}
	else {
		msgbox (RustboroCity_DevonCorp_1F_Text_OnlyAuthorizedPeopleEnter, MSGBOX_DEFAULT)
	}
	release
}

script RustboroCity_DevonCorp_1F_EventScript_Employee {
	lock
	faceplayer
	if (var(VAR_LOOKER_ROCKET) >= 3 && var(VAR_LOOKER_ROCKET) <= 5) {
		msgbox ("Trust me, if I could be a hero here,\nI would.\pI don't have it in me to take on all\nthose goons that ran in!")
	}
	elif (flag(FLAG_RETURNED_DEVON_GOODS)) {
		msgbox (RustboroCity_DevonCorp_1F_Text_SoundsLikeStolenGoodsRecovered, MSGBOX_DEFAULT)
	}
	elif (flag(FLAG_DEVON_GOODS_STOLEN)) {
		msgbox (RustboroCity_DevonCorp_1F_Text_RobberWasntVeryBright, MSGBOX_DEFAULT)
	}
	else {
		msgbox (RustboroCity_DevonCorp_1F_Text_ThoseShoesAreOurProduct, MSGBOX_DEFAULT)
	}
	release
}

script RustboroCity_DevonCorp_1F_EventScript_Greeter {
	lock
	faceplayer
	if (var(VAR_LOOKER_ROCKET) >= 3 && var(VAR_LOOKER_ROCKET) <= 5) {
		msgbox ("No, no, no. This can't be happening.")
	}
	elif (flag(FLAG_RETURNED_DEVON_GOODS)) {
		msgbox (RustboroCity_DevonCorp_1F_Text_WelcomeToDevonCorp, MSGBOX_DEFAULT)
	}
	elif (flag(FLAG_RECOVERED_DEVON_GOODS) || flag(FLAG_DEVON_GOODS_STOLEN)) {
		msgbox (RustboroCity_DevonCorp_1F_Text_StaffGotRobbed, MSGBOX_DEFAULT)
	}
	else {
		msgbox (RustboroCity_DevonCorp_1F_Text_WelcomeToDevonCorp, MSGBOX_DEFAULT)
	}
	release
}

script Looker_Devoncorp_1F {
	lock
	speakername (CommonText_Looker)
	msgbox (format ("You must let me through!\n"
					"I am an officer of the law!\p"
					"Stand down at once!"))
	release
}
