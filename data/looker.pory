script RustboroCity_EventScript_Looker {
	lock
	setflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	addobject (LOOKER_RUSTBORO)
	setobjectxy (LOOKER_RUSTBORO, 21, 15)
	special (SpawnCameraObject)
	applymovement (OBJ_EVENT_ID_CAMERA, Common_Movement_WalkRight2)
	waitmovement (0)
	playbgm (MUS_PL_LOOKER, FALSE)
	applymovement (LOOKER_RUSTBORO, RustboroCity_LookerMove)
	waitmovement (0)
	removeobject (LOOKER_RUSTBORO)
	setvar (VAR_RUSTBORO_CITY_STATE, 8)
	applymovement (OBJ_EVENT_ID_CAMERA, Common_Movement_WalkLeft2)
	waitmovement (0)
	special (RemoveCameraObject)
	clearflag (FLAG_SAFE_FOLLOWER_MOVEMENT)
	fadedefaultbgm
	goto (Rival_Scene03)
}

movement RustboroCity_LookerMove {
	walk_down * 4
	delay_16 * 2
	walk_in_place_faster_up
	delay_16 * 4
	walk_down * 2
	delay_16 * 1
	walk_in_place_right
	delay_16 * 4
	walk_down * 2
	delay_16 * 2
	walk_faster_up * 4
	delay_16 * 4
	lock_facing_direction
	walk_slow_down * 3
	delay_16 * 1
	unlock_facing_direction
	walk_faster_down * 7
}

script SlateportCity_EventScript_Looker {
	lock
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	addobject (LOOKER_SLATEPORT)
	setobjectxy (LOOKER_SLATEPORT, VAR_TEMP_0, 16)
	playbgm (MUS_PL_LOOKER, FALSE)
	applymovement (LOOKER_SLATEPORT, SlateportCity_LookerMove1)
	waitmovement (0)
	playse (SE_SUPER_EFFECTIVE)
	applymovement (LOOKER_SLATEPORT, SlateportCity_LookerMove2)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	waitmovement (0)
	speakername (CommonText_Q)
	msgbox (format ("Ah!\n"
					"My greatest apologies. It seems as though running through the City without paying any mind was a poor choice.\p"
					"I am-"))
	playse (SE_PIN)
	applymovement (LOOKER_SLATEPORT, Common_Movement_ExclamationMark)
	waitmovement (0)
	delay (50)
	msgbox (format ("No!\n"
					"Is it possible that you have already suspected that I am a part of the International Police?"))
	applymovement (LOOKER_SLATEPORT, Common_Movement_WalkInPlaceFasterDown)
	waitmovement (0)
	msgbox (format ("No, this is very bad, for an agent such as myself to have his cover blown so quickly…\p"
					"How could I ever show my face again at headquarters?!"))
	closemessage
	delay (30)
	applymovement (LOOKER_SLATEPORT, Common_Movement_WalkInPlaceFasterUp)
	waitmovement (0)
	speakername (CommonText_Looker)
	msgbox (format ("Very well, you may refer to me as Looker!\p"
					"My name? A codename, yes.\n"
					"An elite agent such as myself must keep many secrets.\p"
					"I am here in Hoenn to root out enemy agents of the world!\p"
					"Team Aqua?\n"
					"Team Magma?\l"
					"I know none of the sort!\p"
					"No, I am on the tail of other evildoers that spread chaos through the world.\p"
					"If you come across any such persons, be sure to make me aware!\p"
					"Farewell, then!"))
	closemessage
	if (var(VAR_TEMP_0) == 19) {
		applymovement (LOOKER_SLATEPORT, Common_Movement_WalkLeft)
	}
	else {
		applymovement (LOOKER_SLATEPORT, Common_Movement_WalkRight)
	}
	waitmovement (0)
	applymovement (LOOKER_SLATEPORT, Common_Movement_WalkUp7)
	waitmovement (0)
	removeobject (LOOKER_SLATEPORT)
	setvar (VAR_SLATEPORT_CITY_STATE, 1)
	fadedefaultbgm
	release
}

movement SlateportCity_LookerMove1 {
	walk_faster_up * 6
}

movement SlateportCity_LookerMove2 {
	lock_facing_direction
	walk_slow_down * 1
	unlock_facing_direction
	delay_16 * 2
}

script Route115_EventScript_Looker {
	lock
	playbgm (MUS_DP_LAKE_EVENT, FALSE)
	pokenavcall (Route115_Text_Looker_Call)
	waitmessage
	delay (30)
	playfanfare (MUS_REGISTER_MATCH_CALL)
	msgbox (MatchCall_Text_RegisteredLooker, MSGBOX_DEFAULT)
	waitfanfare
	closemessage
	delay (30)
	setflag (FLAG_ENABLE_LOOKER_MATCH_CALL)
	setvar (VAR_LOOKER_ROCKET, 1)
	fadedefaultbgm
	release
}

text Route115_Text_Looker_Call {
	"… … … … … Beep!\p"
	"Looker: {PLAYER}!\p"
	"Quickly, I am in dire need of your\n"
	"help!\l"
	"I have managed to track down our\l"
	"adversaries at last!\p"
	"Come to Devoncorp in Rustboro as\n"
	"soon as you are able.\l"
	"The enemy, they've already\l"
	"launched an attack!\p"
	"… … … … … Click!"
}

text MatchCall_Text_RegisteredLooker {
	"Registered Looker\n"
	"in the Pokénav.$"
}


script RustboroCity_EventScript_Looker_2 {
	lock
	playbgm (MUS_PL_LOOKER, FALSE)
	pokenavcall (RustboroCity_Text_Looker_Call)
	waitmessage
	closemessage
	setvar (VAR_LOOKER_ROCKET, 8)
	fadedefaultbgm
	release
}

text RustboroCity_Text_Looker_Call {
	"… … … … … Beep!\p"
	"Looker: {PLAYER}!\p"
	"You are safe, yes?\p"
	"It seems that in my haste,\n"
	"focusing on the safety of\l"
	"civilians while escorting our\l"
	"villainous boss to the police,\l"
	"he gave me the slip!\p"
	"You have not seen him, no?\p"
	"… … … … …\n"
	"No? That is unfortunate.\p"
	"Continue on your journey then.\n"
	"I will begin the manhunt for our\l"
	"escapee.\p"
	"Your job will be to stay safe, and\n"
	"if you sight him, contact me\l"
	"immediately.\p"
	"Stay safe, {PLAYER}!\p"
	"… … … … … Click!"
}

script FortreeCity_EventScript_Looker {
	lock
	speakername (CommonText_Looker)
	msgbox (format ("Greetings, {PLAYER}.\p"
					"I am still on the hunt for Team Rocket,\n"
					"but I will inform you immediately should\l"
					"I get a lead on them."))
	release
}

script SootopolisCity_EventScript_Looker {
	lock
	playbgm (MUS_PL_LOOKER, FALSE)
	pokenavcall (SootopolisCity_Text_Looker_Call)
	waitmessage
	closemessage
	setvar (VAR_LOOKER_ROCKET, 9)
	fadedefaultbgm
	release
}

text SootopolisCity_Text_Looker_Call {
	"… … … … … Beep!\p"
	"Looker: I have discerned the\n"
	"current location of Team Rocket!\p"
	"{PLAYER}, please join me at\n"
	"Trainer Hill on Route 111!\p"
	"There is the location at which we\n"
	"will make our stand against Team\l"
	"Rocket!"
}

//map script
script TrainerHill_Looker1 {
	lock
	speakername (CommonText_Looker)
	msgbox (format ("It is good that you are here.\p"
					"Follow me. We cannot squander any time."))
	closemessage
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOOKER_TRAINERHILL, Common_Movement_WalkLeft)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOOKER_TRAINERHILL, Common_Movement_WalkLeft6)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft6)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
	applymovement (LOOKER_TRAINERHILL, Common_Movement_WalkUp)
	waitmovement (0)
	applymovement (LOOKER_TRAINERHILL, Common_Movement_WalkLeft)
	waitmovement (0)
	applymovement (LOOKER_TRAINERHILL, Common_Movement_WalkInPlaceDown)
	waitmovement (0)
	speakername (CommonText_Looker)
	msgbox (format ("Feast your eyes on this!\p"
					"… … …\n"
					"No, this is not just a bollard, but one\l"
					"with a secret switch hidden on it!\p"
					"Go ahead, press the switch and see for\n"
					"yourself!"))
	closemessage
	setvar (VAR_LOOKER_ROCKET, 10)
	release
}

script TrainerHill_SetLooker {
	if (var(VAR_LOOKER_ROCKET) <= 11 && (var(VAR_LOOKER_ROCKET) >= 10)) {
		clearflag (FLAG_TEMP_1)
		addobject (LOOKER_TRAINERHILL)
		setobjectxyperm (LOOKER_TRAINERHILL, 1, 13)
	}
	elif (var(VAR_LOOKER_ROCKET) == 9) {
		clearflag (FLAG_TEMP_1)
	}
}

//pressing switch
script TrainerHill_LookerSwitch {
	if (var(VAR_LOOKER_ROCKET) == 10) {
		lock
		playse (SE_SWITCH)
		delay (20)
		playse (SE_UNLOCK)
		call (TrainerHill_SetRocketRefuge)
		delay (30)
		playse (SE_PIN)
		applymovement (LOOKER_TRAINERHILL, Common_Movement_ExclamationMark)
		waitmovement (0)
		delay (50)
		speakername (CommonText_Looker)
		msgbox (format ("Yes!\n"
						"Just as I suspected!"))
		msgbox (TrainerHill_Text_LookerGoIn, MSGBOX_DEFAULT)
		setvar (VAR_LOOKER_ROCKET, 11)
		release
	}
}

script TrainerHill_SetRocketRefuge {
	setmetatile (0, 14, 0x3D0, TRUE)
	setmetatile (1, 14, 0x3D1, TRUE)
	setmetatile (2, 14, 0x3D2, FALSE)
	setmetatile (0, 15, 0x3D8, TRUE)
	setmetatile (1, 15, 0x3D9, TRUE)
	setmetatile (2, 15, 0x3DA, FALSE)
	setmetatile (0, 16, 0x3E0, TRUE)
	setmetatile (1, 16, 0x3E1, TRUE)
	setmetatile (2, 16, 0x3E2, FALSE)
	special (DrawWholeMapView)
}

//chooses which script when talking to Looker
script TrainerHill_LookerTalk {
	if (var(VAR_LOOKER_ROCKET) == 10) {
		goto (TrainerHill_Looker2)
	}
	if (var(VAR_LOOKER_ROCKET) == 11) {
		goto (TrainerHill_Looker3)
	}
}

//speaking to looker before pressing button
script TrainerHill_Looker2 {
	lock
	faceplayer
	speakername (CommonText_Looker)
	msgbox (format ("What are you waiting for?\p"
					"Go ahead and press the switch."))
	closemessage
	applymovement (LOOKER_TRAINERHILL, Common_Movement_WalkInPlaceDown)
	waitmovement (0)
	release
}
//speaking to looker before entering refuge
script TrainerHill_Looker3 {
	lock
	faceplayer
	speakername (CommonText_Looker)
	msgbox (TrainerHill_Text_LookerGoIn, MSGBOX_DEFAULT)
	release
}

text TrainerHill_Text_LookerGoIn {
	"Go on inside.\n"
	"I will be right behind you."
}

const LOOKER_ROCKETREFUGE_B1F = 8

script RocketRefuge_B1F_Looker {
	lock
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
	waitmovement (0)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
	waitmovement (0)
	delay (10)
	playse (SE_EXIT)
	addobject (LOOKER_ROCKETREFUGE_B1F)
	applymovement (LOOKER_ROCKETREFUGE_B1F, Common_Movement_FaceRight)
	waitmovement (0)
	setobjectxy (LOOKER_ROCKETREFUGE_B1F, 2, 2)
	speakername (CommonText_Looker)
	msgbox ("Yes. Here we are.")
	closemessage
	applymovement (LOOKER_ROCKETREFUGE_B1F, Common_Movement_WalkInPlaceDown)
	waitmovement (0)
	delay (30)
	applymovement (LOOKER_ROCKETREFUGE_B1F, Common_Movement_WalkInPlaceLeft)
	waitmovement (0)
	delay (30)
	applymovement (LOOKER_ROCKETREFUGE_B1F, Common_Movement_WalkInPlaceRight)
	waitmovement (0)
	speakername (CommonText_Looker)
	msgbox (format ("Now. I will go on ahead, clearing a path for us both.\p"
					"Follow after me, but do be careful.\p"
					"I will attempt to retrieve a disguise, so you may not recognize me when we next meet.\p"
					"That is one of my strengths, after all."))
	closemessage
	applymovement (LOOKER_ROCKETREFUGE_B1F, Common_Movement_WalkInPlaceDown)
	waitmovement (0)
	playbgm (MUS_PL_LOOKER, FALSE)
	delay (40)
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
	applymovement (LOOKER_ROCKETREFUGE_B1F, Common_Movement_WalkFastDown6)
	waitmovement (0)
	speakername (CommonText_Looker)
	playse (SE_M_RAZOR_WIND2)
	delay (6)
	playse (SE_M_RAZOR_WIND2)
	delay (6)
	playse (SE_M_RAZOR_WIND2)
	delay (6)
	playse (SE_M_RAZOR_WIND2)
	delay (6)
	playse (SE_M_RAZOR_WIND2)
	delay (6)
	playse (SE_M_RAZOR_WIND2)
	delay (6)
	playse (SE_M_RAZOR_WIND2)
	msgbox (format ("Ahhhh!"))
	closemessage
	fadedefaultbgm
	delay (20)
	removeobject (LOOKER_ROCKETREFUGE_B1F)
	setvar (VAR_LOOKER_ROCKET, 12)
	release
}


const LOOKER_ROCKETREFUGE_B2F = 6

script RocketRefuge_B2F_Looker {
	lock
	playbgm (MUS_DP_ENCOUNTER_GALACTIC, FALSE)
	applymovement (LOOKER_ROCKETREFUGE_B2F, Common_Movement_ExclamationMark)
	waitmovement (0)
	delay (60)
	applymovement (LOOKER_ROCKETREFUGE_B2F, Common_Movement_WalkLeft4)
	waitmovement (0)
	speakername (CommonText_Q)
	msgbox (format ("Ah!\n"
					"Here you finally are.\p"
					"I've been waiting for your return.\p"
					"I managed to find this uniform, but\nit doesn't seem quite right…\p"
					"Wait, do you not recognize me,\n{PLAYER}?"))
	playbgm (MUS_PL_LOOKER, FALSE)
	speakername (CommonText_Looker)
	msgbox (format ("It is I, Looker, of course!\p"
					"From the look on your face, it seems\nthat I was able to fool you at least.\p"
					"Once I do find the correct uniform,\nI will easily be able to fool the many\lgrunts in this hideaway."))
	msgbox (RocketRefuge_B2F_LookerText, MSGBOX_DEFAULT)
	closemessage
	fadedefaultbgm
	setvar(VAR_LOOKER_ROCKET, 13)
	release
}

script RocketRefuge_B2F_LookerTalk {
	lock
	faceplayer
	speakername (CommonText_Looker)
	msgbox (RocketRefuge_B2F_LookerText, MSGBOX_DEFAULT)
	release
}

text RocketRefuge_B2F_LookerText {
	"Anyway, go on ahead.\n"
	"I will return to you once I am able.\p"
	"This uniform would be a dead giveaway."
}

const GIOVANNI_ROCKETREFUGE_B3F = 3
const LOOKER_ROCKETREFUGE_B3F = 4

script RocketRefuge_B3F_Giovanni {
	faceplayer
	speakername (CommonText_Giovanni)
	trainerbattle_single (TRAINER_GIOVANNI_REFUGE, "So this is what it's come to.\pAll of my men, put down like dogs before\nme.\pAwfully weak, I must think… but no,\nany failures lie at my feet alone.\pNo, I cannot allow the great Team\nRocket's operations in Hoenn to be\lput to an end by a mere child!", "What is it about this region that makes\nexpansion so difficult?", RocketRefuge_B3F_Looker, NO_MUSIC)
}

script RocketRefuge_B3F_Looker {
	speakername (CommonText_Giovanni)
	msgbox (format ("… … … … …\p"
					"My anger is burning ever so strongly\nright now, but my eyes remain on our\lultimate goal.\p"
					"{PLAYER}, you have made it clear that\nTeam Rocket has no right to operate\lwithin Hoenn at our current strength.\p"
					"For now we will abscond, but know\nthat all Pokémon in this very region\lwill belong to us some day.\p"
					"Team Rocket's operations will continue,\nbut may us never meet again!"))
	closemessage
	fadescreen (FADE_TO_BLACK)
	removeobject (GIOVANNI_ROCKETREFUGE_B3F)
	playse (SE_EXIT)
	delay (30)
	fadescreen (FADE_FROM_BLACK)
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	setvar (VAR_OBJ_GFX_ID_A, OBJ_EVENT_GFX_ROCKET_M)
	addobject (LOOKER_ROCKETREFUGE_B3F)
	setobjectxy (LOOKER_ROCKETREFUGE_B3F, 13, 13)
	playbgm (MUS_HG_ENCOUNTER_ROCKET, FALSE)
	speakername (CommonText_Q)
	msgbox ("{PLAYER}!")
	closemessage
	applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkUp3)
	waitmovement (0)
	applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkLeft3)
	waitmovement (0)
	if (var(VAR_TEMP_0) == 12) {
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkUp4)
		waitmovement (0)
		applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkRight)
	}
	elif (var(VAR_TEMP_0) == 13) {
		if (var(VAR_TEMP_1) == 5) {
			applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkUp5)
			waitmovement (0)
		applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
			applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkRight2)
		}
		else {
			applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkUp3)
			waitmovement (0)
		applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
			applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkRight2)
		}
	}
	else {
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkUp3)
		waitmovement (0)
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkRight4)
		waitmovement (0)
		applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceDown)
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_WalkInPlaceUp)
	}
	waitmovement (0)
	speakername (CommonText_Q)
	msgbox (format ("Giovanni, have you defeated him already?\p"
					"Oh, my apologies.\nI did end up finding the correct\ldisguise."
					"No further need for it, I presume?"))
	closemessage
	if (var(VAR_TEMP_0) != 14) {
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceUp)
		waitmovement (0)
	}
	applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceLeft)
	waitmovement (0)
	applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceDown)
	waitmovement (0)
	applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceRight)
	waitmovement (0)
	if (var(VAR_TEMP_0) == 14) {
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceUp)
		waitmovement (0)
	}
	removeobject (LOOKER_ROCKETREFUGE_B3F)
	setvar (VAR_OBJ_GFX_ID_A, OBJ_EVENT_GFX_LOOKER)
	addobject (LOOKER_ROCKETREFUGE_B3F)
	if (var(VAR_TEMP_0) == 14) {
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceUp)
	}
	else {
		applymovement (LOOKER_ROCKETREFUGE_B3F, Common_Movement_FaceRight)
	}
	if (var(VAR_TEMP_0) == 12) {
		setobjectxy (LOOKER_ROCKETREFUGE_B3F, 11, 6)
	}
	elif (var(VAR_TEMP_0) == 13) {
		if (var(VAR_TEMP_1) == 5) {
			setobjectxy (LOOKER_ROCKETREFUGE_B3F, 12, 5)
		}
		else {
			setobjectxy (LOOKER_ROCKETREFUGE_B3F, 12, 7)
		}
	}
	else {
		setobjectxy (LOOKER_ROCKETREFUGE_B3F, 14, 7)
	}
	playbgm (MUS_PL_LOOKER, FALSE)
	speakername (CommonText_Looker)
	msgbox (format ("It is I, Looker, of course!\p"
					"Giovanni has already escaped you,\nI presume.\p"
					"Despair not!\p"
					"This time, I did call for backup.\nIt is likely that the police\lalready have our villain in tow.\p"
					"Though I would relish the chance\nto bring him to custody myself…\p"
					"Regardless, you have done a great\ndeed on your own!\lTruly, I and the police at large owe\lyou a great deal for your help.\p"
					"I had relayed your actions to my\nsuperiors in advance, and though I was\lchided for relying on someone so young,\lI have been given something that I can\lgrant you as reward.\p"
					"Please, take these, {PLAYER}."))
	closemessage
	giveitem (ITEM_NORMAL_TERA_SHARD, 10)
	giveitem (ITEM_BOTTLE_CAP, 1)
	speakername (CommonText_Looker)
	msgbox (format ("And you have a Pokévial, yes?\p"
					"Allow me to increase its maximum\ncapacity for you."))
	closemessage
	pokevialincreasesize
	msgbox (Text_PokevialSizeIncrease, MSGBOX_DEFAULT)
	playfanfare (MUS_OBTAIN_ITEM)
	waitfanfare
	closemessage
	speakername (CommonText_Looker)
	msgbox (format ("That may seem like a great reward to\nyou, but the help you have given us is\linvaluable.\p"
					"Now, hopefully Mr. Rocket remains in\nour custody.\p"
					"But if he does not, I know who I can\nrely on in Hoenn.\p"
					"If you ever do need assistance, you\nhave my number which you can contact.\p"
					"For now, goodbye {PLAYER}!"))
	closemessage
	fadescreen (FADE_TO_BLACK)
	removeobject (LOOKER_ROCKETREFUGE_B3F)
	playse (SE_EXIT)
	delay (30)
	fadescreen (FADE_FROM_BLACK)
	setvar (VAR_LOOKER_ROCKET, 14)
	setflag (FLAG_HIDE_ROCKETS_IN_REFUGE)
	setvar (VAR_TEMP_0, 0)
	fadedefaultbgm
	release
}
