	# State descriptions for VAR_EMERALBODY_RIVAL_BATTLE_STATE
	#  1: Set after winning Battle02
	#  2: Set after winning Battle03
	#  3: Set after winning Battle04
	#  4: Set after winning Battle05

# Harper

const HARPER_BATTLE02 = 4

script Harper_Battle02 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 0
	if (var(VAR_EMERALBODY_RIVAL) == 1) { // Check if Harper is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		addobject (HARPER_BATTLE02)
		playse (SE_EXIT)
		waitse
		applymovement (HARPER_BATTLE02, Common_Movement_WalkUp4)
		waitmovement (0)
		applymovement (HARPER_BATTLE02, Common_Movement_WalkRight4)
		waitmovement (0)
		call (GetRivalForNameBox)
		msgbox (Battle02_HarperText_PreBattle, MSGBOX_DEFAULT)
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_HARPER_2, Battle02_HarperText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox(Battle02_HarperText_PostBattle)
		closemessage
		applymovement (LOCALID_PLAYER, Harper_PlayerMoves)
		waitmovement (0)
		applymovement (HARPER_BATTLE02, Common_Movement_WalkRight9)
		waitmovement (0)
		removeobject (HARPER_BATTLE02)
		fadedefaultbgm
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 1)
		release
	}
}

movement Harper_PlayerMoves {
	face_up
	lock_facing_direction
	walk_down
	unlock_facing_direction
}


# Sakura

const SAKURA_BATTLE02 = 3
const SAKURA_BATTLE03 = 8

script Sakura_Battle02 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 0
	if (var(VAR_EMERALBODY_RIVAL) == 4) { // Check if Sakura is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		addobject (SAKURA_BATTLE02)
		applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceDown)
		waitmovement (0)
		playse (SE_PIN)
		applymovement (LOCALID_PLAYER, Common_Movement_ExclamationMark)
		waitmovement (0)
		delay (60)
		applymovement (SAKURA_BATTLE02, Common_Movement_WalkUp4)
		waitmovement (0)
		playse (SE_LEDGE)
		applymovement (SAKURA_BATTLE02, Common_Movement_JumpUp)
		waitmovement (0)
		playse (SE_M_CUT)
		waitse
		call (GetRivalForNameBox)
		msgbox (Battle02_SakuraText_PreBattle, MSGBOX_DEFAULT)
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_SAKURA_2, Battle02_SakuraText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox(Battle02_SakuraText_PostBattle)
		closemessage
		applymovement (SAKURA_BATTLE02, Common_Movement_WalkDown5)
		waitmovement (0)
		removeobject (SAKURA_BATTLE02)
		fadedefaultbgm
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 1)
		release
	}
}

script Sakura_Battle03 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 1
	lock
	call (Common_EventScript_PlayEmRivalBgm)
	call (GetRivalForNameBox)
	msgbox (Battle03_SakuraText_PreBattle, MSGBOX_DEFAULT)
	call (GetRivalMusic)
	trainerbattle_no_intro (TRAINER_SAKURA_3, Battle03_SakuraText_BattleVictory)
	call (GetRivalForNameBox)
	msgbox(Battle03_SakuraText_PostBattle)
	closemessage
	getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
	if (var(VAR_TEMP_1) == 12) { // Standard bottom entrance
		applymovement (SAKURA_BATTLE03, Common_Movement_WalkRight)
		waitmovement (0)
	}
	applymovement (SAKURA_BATTLE03, Common_Movement_WalkDown)
	waitmovement (0)
	playse (SE_LEDGE)
	applymovement (SAKURA_BATTLE03, Common_Movement_JumpDown2)
	waitmovement (0)
	applymovement (SAKURA_BATTLE03, Common_Movement_WalkDown4)
	waitmovement (0)
	removeobject (SAKURA_BATTLE03)
	setflag (FLAG_TEMP_2)
	fadedefaultbgm
	setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 2)
	delay (30)
	playse (SE_EFFECTIVE)
	setmetatile (13, 11, 0x208, FALSE)
	special (DrawWholeMapView)
	delay (30)
	release
}


# Redd

const REDD_BATTLE02 = 40
const REDD_BATTLE03 = 5

script Redd_Battle02 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 0
	if (var(VAR_EMERALBODY_RIVAL) == 3) { // Check if Redd is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		call (GetRivalForNameBox)
		getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_1) == 58) { // Standard bottom entrance
			switch (var(VAR_TEMP_0)) {
				case 17: // Left side
					applymovement (REDD_BATTLE02, Common_Movement_WalkLeft)
					waitmovement (0)
				case 19: // Right side
					applymovement (REDD_BATTLE02, Common_Movement_WalkRight)
					waitmovement (0)
				default: // Center, no side movement
			}
			applymovement (REDD_BATTLE02, Common_Movement_WalkDown2)
			waitmovement (0)
			msgbox (Battle02_ReddText_PreBattle, MSGBOX_DEFAULT)
		}
		else { // Side entrance
			applymovement (REDD_BATTLE02, Common_Movement_FaceUp)
			waitmovement (0)
			playse (SE_PIN)
			applymovement (REDD_BATTLE02, Common_Movement_ExclamationMark)
			waitmovement (0)
			delay (60)
			if (var(VAR_TEMP_1) == 52) { // Upper tile
				applymovement (REDD_BATTLE02, Common_Movement_WalkUp3)
				waitmovement (0)
			}
			else { // Lower tile
				applymovement (REDD_BATTLE02, Common_Movement_WalkUp2)
				waitmovement (0)
			}
			applymovement (REDD_BATTLE02, Common_Movement_WalkRight)
			waitmovement (0)
			msgbox (Battle02_ReddText_PreBattleAlt, MSGBOX_DEFAULT)
		}
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_REDD_2, Battle02_ReddText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox (Battle02_ReddText_PostBattle)
		closemessage
		applymovement (REDD_BATTLE02, Common_Movement_WalkUp6)
		waitmovement (0)
		removeobject (REDD_BATTLE02)
		fadedefaultbgm
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 1)
		setflag (FLAG_TEMP_2)
		release
	}
}

script Redd_Battle03 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 1
	if (var(VAR_EMERALBODY_RIVAL) == 3) { // Check if Redd is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		call (GetRivalForNameBox)
		addobject (REDD_BATTLE03)
		playse (SE_EXIT)
		waitse
		getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_1) == 7) { // Upper trigger
			applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceRight)
			applymovement (REDD_BATTLE03, Common_Movement_WalkDown4)
			waitmovement (0)
		}
		else { // Lower trigger
			applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceRight)
			applymovement (REDD_BATTLE03, Common_Movement_WalkDown3)
			waitmovement (0)
		}
		applymovement (REDD_BATTLE03, Common_Movement_WalkLeft3)
		waitmovement (0)
		if (var(VAR_TEMP_1) == 7) { // Upper trigger
			applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceDown)
			applymovement (REDD_BATTLE03, Common_Movement_WalkInPlaceUp)
			waitmovement (0)
		}
		else { // Lower trigger
			applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceUp)
			applymovement (REDD_BATTLE03, Common_Movement_WalkInPlaceDown)
			waitmovement (0)
		}
		waitmovement (0)
		msgbox (Battle03_ReddText_PreBattle, MSGBOX_DEFAULT)
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_REDD_3, Battle03_ReddText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox (Battle03_ReddText_PostBattle)
		closemessage
		applymovement (REDD_BATTLE03, Common_Movement_WalkLeft4)
		waitmovement (0)
		if (var(VAR_TEMP_1) == 7) { // Upper trigger
			applymovement (REDD_BATTLE03, Common_Movement_WalkInPlaceDown)
			waitmovement (0)
		}
		else { // Lower trigger
			applymovement (REDD_BATTLE03, Common_Movement_WalkDown)
			waitmovement (0)
		}
		removeobject (REDD_BATTLE03)
		playse (SE_EXIT)
		waitse
		fadedefaultbgm
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 2)
		release
	}
}


# Baron

const BARON_BATTLE02 = 7

script Baron_Battle02 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 0
	if (var(VAR_EMERALBODY_RIVAL) == 0) { // Check if Baron is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		call (GetRivalForNameBox)
		getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_1) == 9) { // Trigger spot
			applymovement (BARON_BATTLE02, Common_Movement_WalkInPlaceUp)
			waitmovement (0)
			delay (50)
			applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceDown)
			applymovement (BARON_BATTLE02, Common_Movement_WalkUp)
			waitmovement (0)
		}
		else {
			faceplayer
		}
		msgbox (Battle02_BaronText_PreBattle, MSGBOX_DEFAULT)
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_BARON_2, Battle02_BaronText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox (Battle02_BaronText_PostBattle)
		closemessage
		fadedefaultbgm
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 1)
		setflag (FLAG_TEMP_2)
		fadescreenswapbuffers (FADE_TO_BLACK)
		removeobject (BARON_BATTLE02)
		fadescreenswapbuffers (FADE_FROM_BLACK)
		release
	}
}


# Jakson

const JAKSON_BATTLE02 = 7
const JAKSON_BATTLE02_MURKROW = 6
const JAKSON_BATTLE03 = 48

script Jakson_Battle02 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 0
	if (var(VAR_EMERALBODY_RIVAL) == 2) { // Check if Jakson is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		delay (30)
		call (GetRivalForNameBox)
		getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_0) == 5) {
			switch (var(VAR_TEMP_1)) {
				case 9: // top
					applymovement (JAKSON_BATTLE02, Common_Movement_WalkUp)
					waitmovement (0)
				case 11:
					applymovement (JAKSON_BATTLE02, Common_Movement_WalkDown)
					waitmovement (0)
				case 12:
					applymovement (JAKSON_BATTLE02, Common_Movement_WalkDown2)
					waitmovement (0)
				case 13:
					applymovement (JAKSON_BATTLE02, Common_Movement_WalkDown3)
					waitmovement (0)
				case 14: // bottom
					applymovement (JAKSON_BATTLE02, Common_Movement_WalkDown4)
					waitmovement (0)
				default: // Center (10), no side movement
			}
			applymovement (JAKSON_BATTLE02, Common_Movement_WalkRight2)
			waitmovement (0)
		}
		msgbox (Battle02_JaksonText_PreBattle, MSGBOX_DEFAULT)
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_JAKSON_2, Battle02_JaksonText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox (Battle02_JaksonText_PostBattle)
		closemessage
		fadescreenswapbuffers (FADE_TO_BLACK)
		removeobject (JAKSON_BATTLE02)
		removeobject (JAKSON_BATTLE02_MURKROW)
		fadescreenswapbuffers (FADE_FROM_BLACK)
		fadedefaultbgm
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 1)
		setflag (FLAG_TEMP_2)
		release
	}
}

script Jakson_Battle02_Block {
	lock
	faceplayer
	msgbox ("The Murkrow sees you trying to sneak\naround and actively blocks your\lefforts.")
	playmoncry (SPECIES_MURKROW, CRY_MODE_NORMAL)
	waitmoncry
	release
}

script Jakson_Battle03 { // VAR_EMERALBODY_RIVAL_BATTLE_STATE == 1
	if (var(VAR_EMERALBODY_RIVAL) == 2) { // Check if Jakson is your rival
		lock
		call (Common_EventScript_PlayEmRivalBgm)
		call (GetRivalForNameBox)
		addobject (JAKSON_BATTLE03)
		getplayerxy (VAR_TEMP_0, VAR_TEMP_1)
		if (var(VAR_TEMP_0) == 31) { // Right spot
			setobjectxy (JAKSON_BATTLE03, 31, 103)
		}
		applymovement (LOCALID_PLAYER, Common_Movement_WalkInPlaceDown)
		applymovement (JAKSON_BATTLE03, Common_Movement_WalkUp5)
		waitmovement (0)
		msgbox (Battle03_JaksonText_PreBattle, MSGBOX_DEFAULT)
		call (GetRivalMusic)
		trainerbattle_no_intro (TRAINER_JAKSON_3, Battle03_JaksonText_BattleVictory)
		call (GetRivalForNameBox)
		msgbox (Battle03_JaksonText_PostBattle)
		closemessage
		setvar (VAR_EMERALBODY_RIVAL_BATTLE_STATE, 2)
		if (var(VAR_TEMP_0) == 31) {
			applymovement (JAKSON_BATTLE03, Common_Movement_WalkLeft)
			waitmovement (0)
		}
		else {
			applymovement (JAKSON_BATTLE03, Common_Movement_WalkRight)
			waitmovement (0)
		}
		applymovement (JAKSON_BATTLE03, Common_Movement_WalkUp7)
		waitmovement (0)
		removeobject (JAKSON_BATTLE03)
		fadedefaultbgm
		release
	}
}
