# RUSTBORO CITY (GYM #1)

script RustboroCity_Gym_EventScript_Roxanne {
	setflag (FLAG_CHALLENGING_TRAINER)
	trainerbattle_single (TRAINER_ROXANNE_1, RustboroCity_Gym_Text_RoxanneIntro, RustboroCity_Gym_Text_RoxanneDefeat, RustboroCity_Gym_EventScript_RoxanneDefeated, NO_MUSIC)
	specialvar (VAR_RESULT, ShouldTryRematchBattle)
	if (var(VAR_RESULT) == TRUE) {
		goto (RustboroCity_Gym_EventScript_RoxanneRematch)
	}
	msgbox (RustboroCity_Gym_Text_RoxannePostBattle, MSGBOX_DEFAULT)
	clearflag (FLAG_CHALLENGING_TRAINER)
	release
}

script RustboroCity_Gym_EventScript_RoxanneDefeated {
	message (RustboroCity_Gym_Text_ReceivedStoneBadge)
	waitmessage
	call (Common_EventScript_PlayGymBadgeFanfare)
	msgbox (RustboroCity_Gym_Text_StoneBadgeInfoTakeThis, MSGBOX_DEFAULT)
	setflag (FLAG_DEFEATED_RUSTBORO_GYM)
	setflag (FLAG_BADGE01_GET)
	setvar (VAR_RUSTBORO_CITY_STATE, 1)
	addvar (VAR_PETALBURG_GYM_STATE, 1)
	setvar (VAR_0x8008, 1)
	call (Common_EventScript_SetGymTrainers) // THIS CLEARS THE TRAINER FLAGS FOR THE TRAINERS IN THE GYM, GYM IS IDENTIFIED BY THE VALUE OF VAR_0x8008. (WHY WOULDN'T YOU JUST CALL RustboroCity_Gym_SetGymTrainers DIRECTLY? WERE THEY THINKING THAT GYM LEADERS COULD BE RANDOMIZED OR SOMETHING? LOL)
	if (var(VAR_PETALBURG_GYM_STATE) == 6) {
		call (Common_EventScript_ReadyPetalburgGymForBattle)
	}
	giveitem (ITEM_TM_SMACK_DOWN)
	msgbox (RustboroCity_Gym_Text_ExplainSmackDown, MSGBOX_DEFAULT)
	release
}

script RustboroCity_Gym_EventScript_RoxanneRematch {
	trainerbattle_rematch_double (TRAINER_ROXANNE_1, RustboroCity_Gym_Text_RoxannePreRematch, RustboroCity_Gym_Text_RoxanneRematchDefeat, RustboroCity_Gym_Text_RoxanneRematchNeedTwoMons)
	msgbox (RustboroCity_Gym_Text_RoxannePostRematch, MSGBOX_AUTOCLOSE)
}

script RustboroCity_Gym_EventScript_RegisterRoxanne {
	lockall
	pokenavcall (RustboroCity_Gym_Text_RoxanneRegisterCall)
	waitmessage
	delay (30)
	playfanfare (MUS_REGISTER_MATCH_CALL)
	msgbox (RustboroCity_Gym_Text_RegisteredRoxanne, MSGBOX_DEFAULT)
	waitfanfare
	closemessage
	delay (30)
	setflag (FLAG_ENABLE_ROXANNE_MATCH_CALL)
	clearflag (FLAG_ENABLE_ROXANNE_FIRST_CALL)
	releaseall
}


text RustboroCity_Gym_Text_RoxanneIntro {
	"Hello, I am Roxanne, the Rustboro\n"
	"Pokémon Gym Leader.\p"
	"I became a Gym Leader so that I might\n"
	"apply what I learned at the Pokémon\l"
	"Trainer's School in battle.\p"
	"Would you kindly demonstrate how you\n"
	"battle, and with which Pokémon?"
}

text RustboroCity_Gym_Text_RoxanneDefeat {
	"So…\n"
	"I lost…\p"
	"It seems that I still have much more\n"
	"to learn…\p"
	"I understand.\p"
	"The Pokémon League's rules state\n"
	"that Trainers are to be given this\l"
	"if they defeat a Gym Leader.\p"
	"Please accept the official Pokémon\n"
	"League Stone Badge."
}

text RustboroCity_Gym_Text_ReceivedStoneBadge {
	"{PLAYER} received the Stone Badge\n"
	"from Roxanne."
}

text RustboroCity_Gym_Text_StoneBadgeInfoTakeThis {
	"The Stone Badge enables the use of\n"
	"Cut outside of battle.\p"
	"Please take this with you, too.$"
}

text RustboroCity_Gym_Text_ExplainSmackDown {
	"That Technical Machine, TM96,\n"
	"contains Smack Down.\p"
	"It not only inflicts damage by throwing\n"
	"rocks, it also grounds flying targets.\p"
	"If you use a TM, it instantly teaches\n"
	"the move to a Pokémon.\p"
	"Remember, a TM can be used multiple\n"
	"times."
}

text RustboroCity_Gym_Text_RoxannePostBattle {
	"Since you are so strong, you should\n"
	"challenge other Gym Leaders.\p"
	"By battling many Trainers, you should\n"
	"learn many things."
}

text RustboroCity_Gym_Text_RoxanneRegisterCall {
	"… … … … … …\n"
	"… … … … … Beep!\p"
	"Roxanne: Oh, hello, {PLAYER}?\n"
	"This is Roxanne of Rustboro City.\p"
	"I heard from Brawly that you're\n"
	"moving up through the ranks of\l"
	"Trainers at an incredible clip.\p"
	"Well, I'd rather not think that\n"
	"I can't beat you.\p"
	"I promise, I will shape up my Gym\n"
	"so that you may challenge us again.\p"
	"I hope you'll be looking forward to\n"
	"that occasion as much as me!\p"
	"… … … … … …\n"
	"… … … … … Click!"
}

text RustboroCity_Gym_Text_RegisteredRoxanne {
	"Registered Gym Leader Roxanne\n"
	"in the Pokénav."
}

text RustboroCity_Gym_Text_RoxannePreRematch {
	"Roxanne: I'm so glad to see you again.\n"
	"I'm Roxanne, the Gym Leader here.\p"
	"I'm sure we've both experienced many\n"
	"battles since we last met.\p"
	"I would like to see how much better\n"
	"we've become. Let us battle!"
}

text RustboroCity_Gym_Text_RoxanneRematchDefeat {
	"Grr…\n"
	"Again, I have lost…"
}

text RustboroCity_Gym_Text_RoxannePostRematch {
	"Roxanne: I still have much to learn\n"
	"when it comes to battling Pokémon.\p"
	"That awareness makes me love battling\n"
	"all that much more!"
}

text RustboroCity_Gym_Text_RoxanneRematchNeedTwoMons {
	"Roxanne: I'm so glad to see you again.\n"
	"I'm Roxanne, the Gym Leader here.\p"
	"I'm sure we've both experienced many\n"
	"battles since we last met.\p"
	"I would like to see how much better\n"
	"we've become…\p"
	"Oh, no…\n"
	"You only have one Pokémon with you?\p"
	"Please return with at least one more\n"
	"Pokémon."
}


# DEWFORD TOWN (GYM #2)
