raw `
Route106_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, Route106_OnTransition
	.byte 0

Route106_OnTransition:
	setflag FLAG_TEMP_1
	call_if_eq VAR_EMERALBODY_RIVAL_STATE, 4, EventScript_Clear_FlagTemp1
	end

Route106_EventScript_TrainerTipsSign::
	msgbox Route106_Text_TrainerTips, MSGBOX_SIGN
	end

Route106_EventScript_Douglas::
	trainerbattle_single TRAINER_DOUGLAS, Route106_Text_DouglasIntro, Route106_Text_DouglasDefeated
	msgbox Route106_Text_DouglasPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_EventScript_Kyla::
	trainerbattle_single TRAINER_KYLA, Route106_Text_KylaIntro, Route106_Text_KylaDefeated
	msgbox Route106_Text_KylaPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_EventScript_Elliot::
	trainerbattle_single TRAINER_ELLIOT_1, Route106_Text_ElliotIntro, Route106_Text_ElliotDefeated, Route106_EventScript_ElliotRegisterMatchCallAfterBattle
	specialvar VAR_RESULT, ShouldTryRematchBattle
	goto_if_eq VAR_RESULT, TRUE, Route106_EventScript_ElliotRematch
	msgbox Route106_Text_ElliotPostBattle, MSGBOX_DEFAULT
	release
	end

Route106_EventScript_ElliotRegisterMatchCallAfterBattle::
	special PlayerFaceTrainerAfterBattle
	waitmovement 0
	msgbox Route106_Text_ElliotRegister, MSGBOX_DEFAULT
	register_matchcall TRAINER_ELLIOT_1
	release
	end

Route106_EventScript_ElliotRematch::
	trainerbattle_rematch TRAINER_ELLIOT_1, Route106_Text_ElliotRematchIntro, Route106_Text_ElliotRematchDefeated
	msgbox Route106_Text_ElliotRematchPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_EventScript_Ned::
	trainerbattle_single TRAINER_NED, Route106_Text_NedIntro, Route106_Text_NedDefeated
	msgbox Route106_Text_NedPostBattle, MSGBOX_AUTOCLOSE
	end

Route106_Text_TrainerTips:
	.string "Trainer Tips\p"
	.string "Advice on catching Pokémon with a Rod:\n"
	.string "Press the A Button if you get a bite.$"
`

const BIANCA = 7

script Route106_ChallengeBallTutorial {
	lock
	addobject (BIANCA)
	setobjectxy (BIANCA, 56, 16)
	speakername (CommonText_Q)
	msgbox ("Heeey!")
	closemessage
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
	applymovement (BIANCA, Route106_BiancaEnter)
	waitmovement (0)
	speakername (CommonText_Bianca)
	msgbox (format ("{PLAYER}, it's good to see you again!\p"
					"Oh! You've gotten a Badge! Wow! Amazing!\p"
					"And that thing over there…"))
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceLeft)
	applymovement (BIANCA, Common_Movement_WalkInPlaceLeft)
	waitmovement (0)
	msgbox (format ("Those blue Pokéball looking things are actually really good items, but there just always seem to be really strong Pokémon nearby protecting them.\p"
					"Most people just call them Challenge Balls!"))
	applymovement (OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceRight)
	waitmovement (0)
	msgbox (format ("They're usually super dangerous, but you're starting to look like you can handle yourself, {PLAYER}!\p"
					"Give it a shot if you've got some time!\p"
					"Anyway, I'll see you around, hopefully!"))
	closemessage
	applymovement (BIANCA, Route106_BiancaExit)
	waitmovement (0)
	removeobject (BIANCA)
	setvar (VAR_ROUTE106_STATE, 1)
	release
}

movement Route106_BiancaEnter {
	walk_left * 4
	walk_down * 2
	walk_left * 3
}

movement Route106_BiancaExit {
	walk_right * 3
	walk_up * 2
	walk_right * 4
}
