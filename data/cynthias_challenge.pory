script Gamecube_CynthiasChallenge {
	msgbox ("It's a battle simulator!\n"
			"Want to use it?", MSGBOX_DEFAULT)
	dynmultichoice (0, 6, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, gText_Yes, gText_Info, gText_No)
	if (var(VAR_RESULT) == 0) {
		setflag (FLAG_NO_MONEY_LOSS_ON_DEFEAT)
		setflag (FLAG_CHALLENGING_TRAINER)
		setvar (VAR_0x8000, MUGSHOT_CYNTHIAS_CHALLENGE)
		callnative (DrawMugshot)
		msgbox ("Test")
		if (!flag(FLAG_BATTLESIM_DISC_1_1)) {
			dynmultipush ("Gym Leaders Part 1", 0)
		}
		else {
			dynmultipush ("Gym Leaders Part 1 - Complete!", 0)
		}
		if (!flag(FLAG_BATTLESIM_DISC_1_2)) {
			dynmultipush ("Gym Leaders Part 2", 1)
		}
		else {
			dynmultipush ("Gym Leaders Part 2 - Complete!", 1)
		}
		if (flag(FLAG_BATTLESIM_DISC_1_1) && flag(FLAG_BATTLESIM_DISC_1_2)) {
			if (!flag(FLAG_BATTLESIM_DISC_1_3)) {
				dynmultipush ("Sinnoh League", 2) // requires completion of the previous two series
			}
			else {
				dynmultipush ("Sinnoh League - Complete!", 2) // requires completion of the previous two series
			}
		}
		dynmultistack (0, 6, FALSE, 3, FALSE, 0, DYN_MULTICHOICE_CB_NONE)
		callnative (ClearMugshot)
		switch (var(VAR_RESULT)) {
			case 0:
				call (CynthiasChallenge_Series1)
			case 1:
				call (CynthiasChallenge_Series2)
			case 2:
				call (CynthiasChallenge_Series3)
			default:
				break
		}
		clearflag (FLAG_NO_MONEY_LOSS_ON_DEFEAT)
		clearflag (FLAG_CHALLENGING_TRAINER)
	}
	elif (var(VAR_RESULT) == 1) {
		call (Gamecube_Info)
	}
	closemessage
}

script CynthiasChallenge_Series1 {
	trainerbattle_no_intro (TRAINER_SIMULATOR_ROARK, "W-what? That can't be!\nMy buffed-up Pokémon!") // Roark
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_GARDENIA, "Amazing!\nYou're very good, aren't you?") // Gardenia
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_MAYLENE, "I shall admit defeat…\nYou are much too strong.") // Maylene
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_WAKE, "Hunwah! It's gone and ended!\nHow will I say this… I want more!\lI wanted to battle a lot more!") // Wake
	special (HealPlayerParty)
	msgbox (Gamecube_Text_Congratulations, MSGBOX_DEFAULT)
	if (!flag(FLAG_BATTLESIM_DISC_1_1)) {
		giveitem (ITEM_STELLAR_TERA_SHARD, 2)
		giveitem (ITEM_EXP_CANDY_L, 2)
		setflag (FLAG_BATTLESIM_DISC_1_1)
	}
	else {
		giveitem (ITEM_BALM_MUSHROOM, 1)
	}
}

script CynthiasChallenge_Series2 {
	trainerbattle_no_intro (TRAINER_SIMULATOR_FANTINA, "You are so fantastically strong.\nI know now why I have lost.") // Fantina
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_BYRON, "Hmm! My sturdy Pokémon, defeated!") // Byron
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_CANDICE, "I must say, I'm warmed up to you!\nI might even admire you a little.") // Candice
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_VOLKNER, "You've got me beat…\nYour desire and the noble way your\lPokémon battled for you…\lI even felt thrilled during our match.\lThat was a very good battle.") // Volkner
	special (HealPlayerParty)
	msgbox (Gamecube_Text_Congratulations, MSGBOX_DEFAULT)
	if (!flag(FLAG_BATTLESIM_DISC_1_2)) {
		giveitem (ITEM_STELLAR_TERA_SHARD, 2)
		giveitem (ITEM_EXP_CANDY_L, 2)
		setflag (FLAG_BATTLESIM_DISC_1_2)
	}
	else {
		giveitem (ITEM_BALM_MUSHROOM, 1)
	}
}

script CynthiasChallenge_Series3 {
	trainerbattle_no_intro (TRAINER_SIMULATOR_AARON, "I will now concede defeat. But I think\nyou came to see how great Bug-type\lPokémon can be. Battling is a deep and\lcomplex affair…") // Aaron
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_BERTHA, "Well! Dear child, I must say, that was\nmost impressive. Your Pokémon believed\lin you and did their best to earn you\lthe win.") // Bertha
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_FLINT, "…! I wasn't expecting this! I wasn't\nlooking down on you… But I didn't think\lfor one second that I'd lose!") // Flint
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_LUCIAN, "I see… You getting past the three\nbefore me was no fluke. Your power\lis real.") // Lucian
	special (HealPlayerParty)
	trainerbattle_no_intro (TRAINER_SIMULATOR_CYNTHIA, "…Just a few moments ago, you were the\nmost powerful challenger. And just now,\lyou became the most powerful of all the\lTrainers.") // Cynthia
	special (HealPlayerParty)
	msgbox (Gamecube_Text_Congratulations, MSGBOX_DEFAULT)
	if (!flag(FLAG_BATTLESIM_DISC_1_3)) {
		giveitem (ITEM_SHINY_LURE, 1)
		giveitem (ITEM_EXP_CANDY_XL, 2)
		setvar (VAR_0x8004, 5)
		special (GiveFrontierBattlePoints)
		msgbox ("{PLAYER} obtained\n3 Battle Points.", MSGBOX_GETPOINTS)
		setflag (FLAG_BATTLESIM_DISC_1_3)
	}
	else {
		giveitem (ITEM_PEARL_STRING, 1)
	}
}

script Gamecube_Info {
	msgbox (format ("In the battle simulator you will use your own party to face a series of consecutive battles.\p"
					"After each battle your party will be healed, but you will have no opportunity to reorganize it.\p"
					"A reward will be given for the completion of each series in a disc.\p"
					"Money will not be lost if you are defeated within the battle simulator.\p"
					"Some discs will require a series to be completed before more within it are unlocked.\p"
					"Find more discs out in the world to be faced with more battles and further rewards!"))
	goto (Gamecube_CynthiasChallenge)
}

text Gamecube_Text_Congratulations {
	"Congratulations. You have completed\n"
	"the current series of battles."
}
