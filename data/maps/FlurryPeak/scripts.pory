const GLASTRIER = 9
const SPECTRIER = 10

mapscripts FlurryPeak_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if(var(VAR_SKY_PILLAR_STATE) < 2) {
			setflag (FLAG_TEMP_2)
			setflag (FLAG_TEMP_3)
		}
		else {
			if(flag(FLAG_CAUGHT_GLASTRIER)) {
				setflag (FLAG_TEMP_2)
				removeobject(GLASTRIER)
			}
			if(flag(FLAG_CAUGHT_SPECTRIER)) {
				setflag (FLAG_TEMP_3)
				removeobject(SPECTRIER)
			}
		}
	}
	MAP_SCRIPT_ON_RESUME {
		if(flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
			removeobject(VAR_LAST_TALKED)
		}
	}
}

script FlurryPeak_Boy {
	lock
	faceplayer
	msgbox(format("Playing in the snow is so much fun!\pBut I wish I had warm clothes like the people down the trail."))
	release
}

script FlurryPeak_Sign {
	msgbox (format("{RIGHT_ARROW} Flurry Peak\n {LEFT_ARROW} Route 104"), MSGBOX_SIGN)
}

script FlurryPeak_EventScript_Candy {
	trainerbattle_single (TRAINER_CANDY, "Are you enjoying the snow?", "No! I slipped!")
	msgbox ("Take your time trekking through\nthe snow.", MSGBOX_AUTOCLOSE)
}

script FlurryPeak_EventScript_Josef {
	trainerbattle_single (TRAINER_JOSEF, "Are you sure you're dressed for\nthis weather?", "Ach! That wind!")
	msgbox ("Even with my gloves on, I can\nbarely feel my hands.", MSGBOX_AUTOCLOSE)
}

script FlurryPeak_EventScript_Gerardo {
	trainerbattle_single (TRAINER_GERARDO, "I wonder where this mountain leads.", "It's snow cold out here!")
	msgbox ("Don't fall on the ice down there.", MSGBOX_AUTOCLOSE)
}

script FlurryPeak_EventScript_Aurelia {
	trainerbattle_single (TRAINER_AURELIA, "I hope you weren't planning on\ngoing in the cave.", "Aw no.")
	msgbox ("Even after you beat me, there's\nstill a rock in the way up there.", MSGBOX_AUTOCLOSE)
}

script FlurryPeak_Man {
	lock
	faceplayer
	msgbox(format("They say that at some nights you can hear the chilling neighs of a horse long since departed from this world."))
	release
}

script BattleGlastrier {
	lock
	faceplayer
	setvar (VAR_LAST_TALKED, GLASTRIER)
	playmoncry(SPECIES_GLASTRIER, CRY_MODE_ENCOUNTER)
	msgbox("Brrrrrriiiiiice!")
	waitmoncry
	seteventmon(SPECIES_GLASTRIER, 50)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	special(BattleSetup_StartLegendaryBattle)
	waitstate
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar (VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
		setflag (FLAG_CAUGHT_GLASTRIER)
	}
	release
}

script BattleSpectrier {
	lock
	faceplayer
	setvar (VAR_LAST_TALKED, SPECTRIER)
	playmoncry(SPECIES_SPECTRIER, CRY_MODE_ENCOUNTER)
	msgbox("Sheeeighhhhd!")
	waitmoncry
	seteventmon(SPECIES_SPECTRIER, 50)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
	special(BattleSetup_StartLegendaryBattle)
	waitstate
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
	specialvar (VAR_RESULT, GetBattleOutcome)
	if (var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
		setflag (FLAG_CAUGHT_SPECTRIER)
	}
	release
}
